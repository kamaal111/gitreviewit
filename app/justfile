set dotenv-load

PROJECT := "GitReviewIt.xcodeproj"
SCHEME := "GitReviewIt"
MACOS_PLATFORM := "platform=macOS"
PAYLOAD := "payload"
PKG_PAYLOAD := PAYLOAD / "Applications"
OUTPUT := "out"
APP_NAME := "GitReviewIt"
APP_PATH := OUTPUT / APP_NAME + ".app"
ARCHIVE_PATH := OUTPUT / APP_NAME + ".xcarchive"
MACOS_SDK := "macosx"
RESOURCES := "Resources"
EXPORT_OPTIONS := RESOURCES / "ExportOptions.plist"
COMPONENT := RESOURCES / "Component.plist"
PKG_SCRIPTS := RESOURCES / "PKGScripts"
PKG_INSTALL_LOCATION := "/Applications"
NOTARY_PROFILE := "AC_NOTARY_PROFILE"
RELEASE_CONFIGURATION := "Release"

# Default recipe - shows available commands
default:
    @just --list --unsorted

# Create .pkg
create-pkg: create-app
    #!/bin/zsh

    if [[ -z "$INSTALLER_CERTIFICATE_NAME" ]]
    then
      echo "âŒ INSTALLER_CERTIFICATE_NAME is empty. Set it to your 'Developer ID Installer: â€¦' certificate name."
      security find-identity -p codesigning -v | grep "Developer ID Installer" || true
      exit 1
    fi

    mkdir -p {{ PKG_PAYLOAD }}
    cp -R {{ APP_PATH }} "{{ PKG_PAYLOAD }}/"

    export VERSION=$(/usr/libexec/PlistBuddy -c "Print :CFBundleShortVersionString" "{{ APP_PATH }}/Contents/Info.plist")
    export PKG_ID=$(/usr/libexec/PlistBuddy -c "Print :CFBundleIdentifier" "{{ APP_PATH }}/Contents/Info.plist")
    export PKG_PATH="{{ OUTPUT }}/{{ SCHEME }}.pkg"

    echo "ðŸ” Running codesign checks on .app..."
    codesign --verify --deep --strict --verbose=2 {{ APP_PATH }}

    echo "ðŸ“¦ Building signed .pkg..."
    typeset -a PKGBUILD_ARGS
    PKGBUILD_ARGS=(
        --root {{ PKG_PAYLOAD }}
        --component-plist {{ COMPONENT }}
        --install-location {{ PKG_INSTALL_LOCATION }}
        --identifier "$PKG_ID"
        --version "$VERSION"
        --sign "$INSTALLER_CERTIFICATE_NAME"
        --timestamp
    )

    if [[ -d {{ PKG_SCRIPTS }} ]]; then
      echo "ðŸ§© Including installer scripts from '{{ PKG_SCRIPTS }}'"
      PKGBUILD_ARGS+=(--scripts {{ PKG_SCRIPTS }})
    fi

    pkgbuild "${PKGBUILD_ARGS[@]}" "$PKG_PATH"

    echo "ðŸ§¾ pkg signature:"
    pkgutil --check-signature "$PKG_PATH" || exit 1

    echo "ðŸ“¨ Notarizing .pkg..."
    xcrun notarytool submit "$PKG_PATH" --keychain-profile {{ NOTARY_PROFILE }} --wait || exit 1

    echo "ðŸ“Ž Stapling notarization ticket to .pkg..."
    xcrun stapler staple "$PKG_PATH" || exit 1

    echo "ðŸ” Verifying Gatekeeper..."
    spctl --assess --type install -vv "$PKG_PATH" || exit 1
    xcrun stapler validate "$PKG_PATH" || exit 1

    echo "âœ… Done"
    echo "ðŸ“¦ PKG: $PKG_PATH"

# Create app
create-app: archive
    #!/bin/zsh

    set -o pipefail && xcodebuild -exportArchive \
        -archivePath {{ ARCHIVE_PATH }} \
        -exportOptionsPlist {{ EXPORT_OPTIONS }} \
        -exportPath {{ OUTPUT }} | xcpretty
# Archive app
archive: install-xcpretty clean-artifacts prepare-output
    #!/bin/zsh

    set -o pipefail && xcodebuild archive \
        -project {{ PROJECT }} \
        -scheme {{ SCHEME }} \
        -configuration {{ RELEASE_CONFIGURATION }} \
        -archivePath {{ ARCHIVE_PATH }} \
        -sdk {{ MACOS_SDK }} \
        -destination {{ MACOS_PLATFORM }} | xcpretty

# Clean and build the project
clean-build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean build | xcpretty

# Run tests
test:
    xcodebuild -project {{ PROJECT }} -scheme {{ SCHEME }} \
        -destination {{ MACOS_PLATFORM }} test

# Clean the build artifacts
clean: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean | xcpretty

# Open the project in Xcode
open:
    open {{ PROJECT }}

# Lint the code
lint:
    swiftlint lint --config ../.swiftlint.yml --no-cache --strict

# Bootstrap app for development
bootstrap: install-tools

# Generate Sparkle update keys for app signing
generate-sparkle-keys: build
    #!/bin/zsh

    SPARKLE_BIN_PATH=$(just get-sparkle-binary-path | tail -n 1)
    $SPARKLE_BIN_PATH/generate_keys

[private]
build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} \
        -configuration {{ RELEASE_CONFIGURATION }} build | xcpretty

[private]
get-sparkle-binary-path:
    zsh Scripts/get-sparkle-binary-path.zsh

[private]
prepare-output:
    #!/bin/zsh

    mkdir -p {{ OUTPUT }}

[private]
clean-artifacts:
    #!/bin/zsh

    rm -rf $PAYLOAD {{ OUTPUT }} || exit 1

[private]
install-tools: install-xcpretty

[private]
install-xcpretty:
    #!/bin/zsh

    if ! command -v xcpretty &> /dev/null
    then
        echo "ðŸ“¦ xcpretty is not installed. Installing via Gem..."
        gem install create-dmg || exit 1
    fi
