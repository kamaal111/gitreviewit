set dotenv-load

PROJECT := "GitReviewIt.xcodeproj"
SCHEME := "GitReviewIt"
MACOS_PLATFORM := "platform=macOS"
PAYLOAD := "payload"
PKG_PAYLOAD := PAYLOAD / "Applications"
OUTPUT := "out"
APP_NAME := "GitReviewIt"
APP_PATH := OUTPUT / APP_NAME + ".app"
ARCHIVE_PATH := OUTPUT / APP_NAME + ".xcarchive"
MACOS_SDK := "macosx"
RESOURCES := "Resources"
EXPORT_OPTIONS := RESOURCES / "ExportOptions.plist"
COMPONENT := RESOURCES / "Component.plist"
PKG_SCRIPTS := RESOURCES / "PKGScripts"
PKG_INSTALL_LOCATION := "/Applications"
NOTARY_PROFILE := "AC_NOTARY_PROFILE"
RELEASE_CONFIGURATION := "Release"
APPCAST_PATH := "../web/public/appcast.xml"

# Default recipe - shows available commands
default:
    @just --list --unsorted

# Create .pkg
create-pkg: create-app
    #!/bin/zsh

    if [[ -z "$INSTALLER_CERTIFICATE_NAME" ]]
    then
      echo "‚ùå INSTALLER_CERTIFICATE_NAME is empty. Set it to your 'Developer ID Installer: ‚Ä¶' certificate name."
      security find-identity -p codesigning -v | grep "Developer ID Installer" || true
      exit 1
    fi

    mkdir -p {{ PKG_PAYLOAD }}
    cp -R {{ APP_PATH }} "{{ PKG_PAYLOAD }}/"

    export VERSION=$(/usr/libexec/PlistBuddy -c "Print :CFBundleShortVersionString" "{{ APP_PATH }}/Contents/Info.plist")
    export PKG_ID=$(/usr/libexec/PlistBuddy -c "Print :CFBundleIdentifier" "{{ APP_PATH }}/Contents/Info.plist")
    export PKG_PATH="{{ OUTPUT }}/{{ SCHEME }}.pkg"

    echo "üîè Running codesign checks on .app..."
    codesign --verify --deep --strict --verbose=2 {{ APP_PATH }}

    echo "üì¶ Building signed .pkg..."
    typeset -a PKGBUILD_ARGS
    PKGBUILD_ARGS=(
        --root {{ PKG_PAYLOAD }}
        --component-plist {{ COMPONENT }}
        --install-location {{ PKG_INSTALL_LOCATION }}
        --identifier "$PKG_ID"
        --version "$VERSION"
        --sign "$INSTALLER_CERTIFICATE_NAME"
        --timestamp
    )

    if [[ -d {{ PKG_SCRIPTS }} ]]; then
      echo "üß© Including installer scripts from '{{ PKG_SCRIPTS }}'"
      PKGBUILD_ARGS+=(--scripts {{ PKG_SCRIPTS }})
    fi

    pkgbuild "${PKGBUILD_ARGS[@]}" "$PKG_PATH"

    echo "üßæ pkg signature:"
    pkgutil --check-signature "$PKG_PATH" || exit 1

    echo "üì® Notarizing .pkg..."
    xcrun notarytool submit "$PKG_PATH" --keychain-profile {{ NOTARY_PROFILE }} --wait || exit 1

    echo "üìé Stapling notarization ticket to .pkg..."
    xcrun stapler staple "$PKG_PATH" || exit 1

    echo "üîç Verifying Gatekeeper..."
    spctl --assess --type install -vv "$PKG_PATH" || exit 1
    xcrun stapler validate "$PKG_PATH" || exit 1

    echo "‚úÖ Done"
    echo "üì¶ PKG: $PKG_PATH"

    just sign-pkg

# Create app
create-app: archive
    #!/bin/zsh

    set -o pipefail && xcodebuild -exportArchive \
        -archivePath {{ ARCHIVE_PATH }} \
        -exportOptionsPlist {{ EXPORT_OPTIONS }} \
        -exportPath {{ OUTPUT }} | xcpretty

# Archive app
archive: install-xcpretty clean-artifacts prepare-output clean
    #!/bin/zsh

    set -o pipefail && xcodebuild archive \
        -project {{ PROJECT }} \
        -scheme {{ SCHEME }} \
        -configuration {{ RELEASE_CONFIGURATION }} \
        -archivePath {{ ARCHIVE_PATH }} \
        -sdk {{ MACOS_SDK }} \
        -destination {{ MACOS_PLATFORM }} | xcpretty

# Clean and build the project
clean-build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean build | xcpretty

# Run tests
test:
    xcodebuild -project {{ PROJECT }} -scheme {{ SCHEME }} \
        -destination {{ MACOS_PLATFORM }} test

# Clean the build artifacts
clean: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} clean | xcpretty

# Open the project in Xcode
open:
    open {{ PROJECT }}

# Lint the code
lint:
    swiftlint lint --config ../.swiftlint.yml --no-cache --strict

# Bootstrap app for development
bootstrap: install-tools

# Generate Sparkle update keys for app signing
generate-sparkle-keys: build
    #!/bin/zsh

    SPARKLE_BIN_PATH=$(just get-sparkle-binary-path | tail -n 1)
    $SPARKLE_BIN_PATH/generate_keys

# Store NotaryTool credentials for notarization
create-notary-profile:
    #!/bin/zsh

    xcrun notarytool store-credentials "{{ NOTARY_PROFILE }}" \
        --apple-id "$APPLE_ID_EMAIL" \
        --team-id "$APPLE_TEAM_ID" \
        --password "$APP_SPECIFIC_PASSWORD"

[private]
sign-pkg:
    #!/bin/zsh

    just validate-pkg-output

    pkg_files=($(find {{ OUTPUT }} -maxdepth 1 -name "*.pkg" -type f))
    SPARKLE_BIN_PATH=$(just get-sparkle-binary-path | tail -n 1)
    SIGN_UPDATE_PATH="$SPARKLE_BIN_PATH/sign_update"

    if [[ ! -f "$SIGN_UPDATE_PATH" ]]
    then
        echo "‚ùå sign_update binary not found at: $SIGN_UPDATE_PATH"
        exit 1
    fi

    echo "üîê Signing .pkg files with Sparkle..."

    for pkg_file in "${pkg_files[@]}"; do
        filename=$(basename "$pkg_file")
        echo "üìù Signing: $filename"

        signature_output=$("$SIGN_UPDATE_PATH" "$pkg_file")

        if [[ $? -eq 0 ]]
        then
            echo "$signature_output"
            echo "‚úÖ Successfully signed: $filename"
            just update-appcast-from-signature "$signature_output"
        else
            echo "‚ùå Failed to sign: $filename"
            exit 1
        fi
    done

    echo "‚úÖ All .pkg files signed successfully"

[private]
validate-pkg-output:
    #!/bin/zsh

    if [[ ! -d {{ OUTPUT }} ]]
    then
        echo "‚ùå Output directory '{{ OUTPUT }}' does not exist"
        exit 1
    fi

    pkg_files=($(find {{ OUTPUT }} -maxdepth 1 -name "*.pkg" -type f))

    if [[ ${#pkg_files[@]} -eq 0 ]]
    then
        echo "‚ùå No .pkg files found in '{{ OUTPUT }}' directory"
        exit 1
    fi

[private]
update-appcast-from-signature signature_output:
    #!/bin/zsh

    signature_output='{{ signature_output }}'

    ed_signature=$(echo "$signature_output" | grep -o 'sparkle:edSignature="[^"]*"' | sed 's/sparkle:edSignature="\(.*\)"/\1/')
    length=$(echo "$signature_output" | grep -o 'length="[0-9]*"' | sed 's/length="\([0-9]*\)"/\1/')

    if [[ -z "$ed_signature" || -z "$length" ]]
    then
        echo "‚ö†Ô∏è  Could not extract signature or length from sign_update output"
        exit 0
    fi

    echo "üîë Extracted signature: $ed_signature"
    echo "üìè Extracted length: $length"

    if [[ ! -f {{ APPCAST_PATH }} ]]
    then
        echo "‚ö†Ô∏è  appcast.xml not found at: {{ APPCAST_PATH }}"
        exit 0
    fi

    version=$(/usr/libexec/PlistBuddy -c "Print :CFBundleShortVersionString" "{{ APP_PATH }}/Contents/Info.plist")
    build_number=$(/usr/libexec/PlistBuddy -c "Print :CFBundleVersion" "{{ APP_PATH }}/Contents/Info.plist")
    
    if [[ -z "$version" || -z "$build_number" ]]
    then
        echo "‚ö†Ô∏è  Could not extract version or build number from app"
        exit 1
    fi
    
    echo "üì¶ Version: $version (Build: $build_number)"
    
    # Generate pubDate (1 hour from now in RFC 822 format)
    pub_date=$(date -u -v+1H '+%a, %d %b %Y %H:%M:%S +0000')
    echo "üìÖ Pub date: $pub_date"
    
    download_url="https://github.com/kamaal111/gitreviewit/releases/download/$version/GitReviewIt.pkg"
    echo "üîó Download URL: $download_url"

    echo "üìù Updating appcast.xml..."

    sed -i '' -E \
        -e 's|<title>Version [^<]*</title>|<title>Version '"$version"'</title>|' \
        -e 's|<sparkle:version>[0-9]+</sparkle:version>|<sparkle:version>'"$build_number"'</sparkle:version>|' \
        -e 's|<sparkle:shortVersionString>[^<]*</sparkle:shortVersionString>|<sparkle:shortVersionString>'"$version"'</sparkle:shortVersionString>|' \
        -e 's|<pubDate>[^<]*</pubDate>|<pubDate>'"$pub_date"'</pubDate>|' \
        -e 's|url="https://github.com/kamaal111/gitreviewit/releases/download/[^/]*/GitReviewIt.pkg"|url="'"$download_url"'"|' \
        -e 's|sparkle:edSignature="[^"]*"|sparkle:edSignature="'"$ed_signature"'"|' \
        -e 's|length="[0-9]*"|length="'"$length"'"|' \
        {{ APPCAST_PATH }}

    echo "‚úÖ Updated appcast.xml with version $version (build $build_number), signature, and length"

[private]
build: install-xcpretty
    set -o pipefail && xcodebuild -project {{ PROJECT }} \
        -scheme {{ SCHEME }} -destination {{ MACOS_PLATFORM }} \
        -configuration {{ RELEASE_CONFIGURATION }} build | xcpretty

[private]
get-sparkle-binary-path:
    zsh Scripts/get-sparkle-binary-path.zsh

[private]
prepare-output:
    #!/bin/zsh

    mkdir -p {{ OUTPUT }}

[private]
clean-artifacts:
    #!/bin/zsh

    rm -rf $PAYLOAD {{ OUTPUT }} || exit 1

[private]
install-tools: install-xcpretty

[private]
install-xcpretty:
    #!/bin/zsh

    if ! command -v xcpretty &> /dev/null
    then
        echo "üì¶ xcpretty is not installed. Installing via Gem..."
        gem install create-dmg || exit 1
    fi
